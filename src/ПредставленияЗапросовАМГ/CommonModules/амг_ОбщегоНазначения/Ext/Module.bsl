
#Область ПредставленияЗапросов

Функция ПолучитьСоздатьЗапросПредставления(Запрос, Схема = Неопределено, ЗапросПредставления = Неопределено) Экспорт
	
	Если ЗапросПредставления = Неопределено Тогда
	
		Схема = Новый СхемаЗапроса;
		
		Схема.УстановитьТекстЗапроса(Запрос.Текст);
		
		ЗапросПредставления = ПоследнийЗапросВыбораСхемыЗапроса(Схема);
	
	КонецЕсли;
	
	Возврат ЗапросПредставления;
	
КонецФункции

Функция ПоследнийЗапросВыбораСхемыЗапроса(Схема) Экспорт
	
	ПоследнийЗапросВыбора = Неопределено;
	
	КоличествоЗапросов = Схема.ПакетЗапросов.Количество();
	
	Для НомерЗапроса = 1 По КоличествоЗапросов Цикл
		
		ПоследнийЗапросВыбора = Схема.ПакетЗапросов[КоличествоЗапросов - НомерЗапроса];
		
		Если ТипЗнч(ПоследнийЗапросВыбора) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПоследнийЗапросВыбора;
	
КонецФункции

Процедура ЗапросПредставленияДобавитьНедостающиеСведения(Запрос, ПараметрыПостроения, ИмяВТПредставления, ИменаСведений) Экспорт
	
	Перем ИсточникиСведений;
	
	Если ИменаСведений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьИсточникиСведений = ПараметрыПостроения.Свойство("ИсточникиСведений", ИсточникиСведений);

	ОбновитьЗапросПоСхеме = Ложь;
	
	Схема = Новый СхемаЗапроса;
	Схема.УстановитьТекстЗапроса(Запрос.Текст);
	
	ЗапросПредставления = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросСхемыФормирующийВТ(Схема, ИмяВТПредставления);
	
	Если ЗапросПредставления <> Неопределено Тогда
		
		Для Каждого ИмяСведения Из ИменаСведений Цикл
			
			Если ЗапросПредставления.Колонки.Найти(ИмяСведения) = Неопределено Тогда
				
				ОбновитьЗапросПоСхеме = Истина;
				
				Если ЕстьИсточникиСведений Тогда
					ИмяПоля = ИсточникиСведений[ИмяСведения];
					Если ИмяПоля = Неопределено Тогда
						ИмяПоля = ИмяСведения;
					КонецЕсли;
				Иначе
					ИмяПоля = ИмяСведения;
				КонецЕсли;
				
				ЗапросПредставления.Операторы[0].ВыбираемыеПоля.Добавить(ИмяПоля); 
				
				ЗапросПредставления.Колонки[ЗапросПредставления.Колонки.Количество() - 1].Псевдоним = ИмяСведения; // может не совпадать с именем поля в источнике
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбновитьЗапросПоСхеме Тогда
		Запрос.Текст = Схема.ПолучитьТекстЗапроса();
	КонецЕсли;
	
КонецПроцедуры
	
Процедура ДополнитьИменаСведенийПсевдонимамиИсточников(Запрос, ИменаСведений, ИсточникиСведений) Экспорт
	
	Перем Схема;
	
	ЗапросПредставления = ПолучитьСоздатьЗапросПредставления(Запрос, Схема);
	
	Для каждого Источник Из ЗапросПредставления.Операторы[0].Источники Цикл
		
		Если Источник.Источник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПсевдонимИсточника = Источник.Источник.Псевдоним;
		
		ЗапросВТИсточника = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросСхемыФормирующийВТ(Схема, "ВТ" + ПсевдонимИсточника);
		
		Если ЗапросВТИсточника = Неопределено Тогда
			Контейнер = Источник.Источник.ДоступныеПоля;
			ИмяРеквизита = "Имя";
		Иначе
			Контейнер = ЗапросВТИсточника.Колонки;
			ИмяРеквизита = "Псевдоним";
		КонецЕсли;
	
		Для каждого Элемент Из Контейнер Цикл 
			
			ИмяСведения = Элемент[ИмяРеквизита];
			
			Если ЗапросПредставления.Колонки.Найти(ИмяСведения) = Неопределено Тогда
				
				ДополнитьИмяСведенияПсевдонимомИсточника(ИменаСведений, ИсточникиСведений, ИмяСведения, ПсевдонимИсточника)
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьИмяСведенияПсевдонимомИсточника(ИменаСведений, ИсточникиСведений, ИмяСведения, ПсевдонимИсточника) Экспорт

	ИндексСведения = ИменаСведений.Найти(ИмяСведения);
	
	Если ИндексСведения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИсточникиСведений.Вставить(ИмяСведения, СтрШаблон("%1.%2", ПсевдонимИсточника, ИмяСведения));
	
КонецПроцедуры

#КонецОбласти
