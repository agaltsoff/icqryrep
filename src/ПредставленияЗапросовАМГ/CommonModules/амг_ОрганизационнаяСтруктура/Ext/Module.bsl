
#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПсотроенияВТПодчиненностьПодразделений1234(Подразделения) Экспорт
	
	ПараметрыПостроения = ОбщиеПараметрыПостроенияВТПодчиненностьПодразделений1234();
	
	ПараметрыПостроения.Вставить("Подразделения", Подразделения);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

Функция ПараметрыПостроенияВТПодчиненностьПодразделений1234ПоТаблицеФильтра(ИмяВТФильтраПодразделений, ИмяПоляПодразделение = "Подразделение") Экспорт
	
	ПараметрыПостроения = ОбщиеПараметрыПостроенияВТПодчиненностьПодразделений1234();
	
	ПараметрыПостроения.Вставить("ИмяВТФильтраПодразделений", ИмяВТФильтраПодразделений);
	ПараметрыПостроения.Вставить("ИмяПоляПодразделение", ИмяПоляПодразделение);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

#КонецОбласти

#Область ПолучениеСведенийПодчиненностиПодразделений

Функция ОбщиеПараметрыПостроенияВТПодчиненностьПодразделений1234()
	
	ПараметрыПостроения = Новый Структура;
	ПараметрыПостроения.Вставить("Отборы", Новый Массив);
	
	ПараметрыПостроения.Вставить("ВключаяРасформированные", Ложь);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Представление_ПодчиненностьПодразделений1234
	
Функция ЗапросПредставленияПодчиненностьПодразделений1234(ТолькоРазрешенные, Подразделения, ИменаСведений = Неопределено, ИмяВТПредставления_ПодчиненностьПодразделений = "Представления_ПодчиненностьПодразделений1234", ИмяВТОтбораПодразделений = "", ПараметрыЗапроса = Неопределено) Экспорт

	Если Не ПустаяСтрока(ИмяВТОтбораПодразделений) Тогда
		ПараметрыПостроения = ПараметрыПостроенияВТПодчиненностьПодразделений1234ПоТаблицеФильтра(ИмяВТОтбораПодразделений);
	Иначе
		ПараметрыПостроения = ПараметрыПсотроенияВТПодчиненностьПодразделений1234(Подразделения);
	КонецЕсли;
	
	Если ПараметрыЗапроса <> Неопределено Тогда
		
		ПараметрыПостроения.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
		
		ПредставлениеВключаяРасформированные = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("ВключаяРасформированные", ПараметрыЗапроса);
		ПараметрыПостроения.Вставить("ВключаяРасформированные", ВРег(ПредставлениеВключаяРасформированные) = "ИСТИНА" );
		
	КонецЕсли;
	
	Запрос = ЗапросВТПодчиненностьПодразделений1234(ТолькоРазрешенные, ПараметрыПостроения, ИменаСведений, ИмяВТПредставления_ПодчиненностьПодразделений);
	
	амг_ОбщегоНазначения.ЗапросПредставленияДобавитьНедостающиеСведения(Запрос, ПараметрыПостроения, ИмяВТПредставления_ПодчиненностьПодразделений, ИменаСведений);
	
	Возврат Запрос;
	
КонецФункции

//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеВерхнегоУровня, // корень иерархии подразделения
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Блок, // третье из существующих подразделений вверх по иерархии не включая корень
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение1, // Подразделение.Родитель.Родитель.Родитель
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение2, // Подразделение.Родитель.Родитель
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение3, // Подразделение.Родитель
//ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение4  // Подразделение
Функция ЗапросВТПодчиненностьПодразделений1234(ТолькоРазрешенные, ПараметрыПостроения, Знач ИменаСведений = Неопределено, Знач ИмяВТПодчиненностьПодразделений1234 = "ВТПодчиненностьПодразделений1234") Экспорт
	
	ПоТаблицеФильтра = ПараметрыПостроения.Свойство("ИмяВТФильтраПодразделений");

	Запрос = Новый Запрос;
	
	Если ПоТаблицеФильтра Тогда
	
		Запрос.Текст =
#Область ТекстЗапроса
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница КАК Подразделение,
			|	ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница КАК ВышестоящееПодразделение,
			|	ПодчиненностьСтруктурныхЕдиниц.Уровень КАК Уровень
			|ПОМЕСТИТЬ ВТПодчиненностьПодразделений
			|ИЗ
			|	РегистрСведений.ПодчиненностьСтруктурныхЕдиниц КАК ПодчиненностьСтруктурныхЕдиниц
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборПодразделений КАК ОтборПодразделений
			|		ПО ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница = ОтборПодразделений.Подразделение"; 
#КонецОбласти
		
	Иначе
	
		Запрос.Текст =
#Область ТекстЗапроса
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница КАК Подразделение,
			|	ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница КАК ВышестоящееПодразделение,
			|	ПодчиненностьСтруктурныхЕдиниц.Уровень КАК Уровень
			|ПОМЕСТИТЬ ВТПодчиненностьПодразделений
			|ИЗ
			|	РегистрСведений.ПодчиненностьСтруктурныхЕдиниц КАК ПодчиненностьСтруктурныхЕдиниц
			|ГДЕ
			|	ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница В(&Подразделения)";
#КонецОбласти
	
	КонецЕсли; 
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст,
#Область ТекстЗапроса
		"ВЫБРАТЬ
		|	Подразделения.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТПодразделения
		|ИЗ
		|	ВТПодчиненностьПодразделений КАК Подразделения
		|ГДЕ
		|	Подразделения.Уровень = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПодчиненностьПодразделений.Подразделение КАК Подразделение,
		|	МАКСИМУМ(ВТПодчиненностьПодразделений.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ВТВерхнииеУровни
		|ИЗ
		|	ВТПодчиненностьПодразделений КАК ВТПодчиненностьПодразделений
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПодчиненностьПодразделений.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПодчиненностьПодразделений1234.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТПодчиненностьПодразделений1234
		|ИЗ
		|	ВТПодразделения КАК ВТПодчиненностьПодразделений1234
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодчиненностьПодразделений КАК ПодразделенияВерхнегоУровня
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВерхнииеУровни КАК ВерхнииеУровни
		|			ПО ПодразделенияВерхнегоУровня.Подразделение = ВерхнииеУровни.Подразделение
		|				И ПодразделенияВерхнегоУровня.Уровень = ВерхнииеУровни.Уровень
		|		ПО ВТПодчиненностьПодразделений1234.Подразделение = ПодразделенияВерхнегоУровня.Подразделение
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодчиненностьПодразделений КАК Подразделения3
		|		ПО ВТПодчиненностьПодразделений1234.Подразделение = Подразделения3.Подразделение
		|			И (Подразделения3.Уровень = 1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодчиненностьПодразделений КАК Подразделения2
		|		ПО ВТПодчиненностьПодразделений1234.Подразделение = Подразделения2.Подразделение
		|			И (Подразделения2.Уровень = 2)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодчиненностьПодразделений КАК Подразделения1
		|		ПО ВТПодчиненностьПодразделений1234.Подразделение = Подразделения1.Подразделение
		|			И (Подразделения1.Уровень = 3)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодчиненностьПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВерхнииеУровни"
	); 
#КонецОбласти
	
	Если СтрЗаканчиваетсяНа(ВРег(ИмяВТПодчиненностьПодразделений1234), "ОРГАНИЗАЦИЙ1234") Тогда
		ДоработатьЗапросДляПодчиненностиПодразделенийОрганизаций(Запрос.Текст)	
	КонецЕсли;
	
	Схема = Неопределено;
	ЗапросПредставления = Неопределено;
	
	// Обработка параметров построения требующих изменения текста запроса
	
	Если ИмяВТПодчиненностьПодразделений1234 = "Представления_ПодчиненностьПодразделенийОрганизаций1234" 
		И Не ПараметрыПостроения.ВключаяРасформированные 
	Тогда
	
		амг_ОбщегоНазначения.ПолучитьСоздатьЗапросПредставления(Запрос, Схема, ЗапросПредставления);
		
		ЗапросВТПодразделения = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросСхемыФормирующийВТ(Схема, "ВТПодразделения");
		
		ЗапросВТПодразделения.Операторы[0].Отбор.Добавить("НЕ Подразделения.Подразделение.Расформировано");
		
	КонецЕсли;
	
	Если Схема <> Неопределено Тогда
		Запрос.Текст = Схема.ПолучитьТекстЗапроса();
	КонецЕсли;
		
	Если ПоТаблицеФильтра И ПараметрыПостроения.ИмяВТФильтраПодразделений <> "ВТОтборПодразделений" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборПодразделений", ПараметрыПостроения.ИмяВТФильтраПодразделений);
	КонецЕсли;
	
	Если Не ПоТаблицеФильтра Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Подразделения", ПараметрыПостроения.Подразделения);
	КонецЕсли;
	
	Если ИмяВТПодчиненностьПодразделений1234 <> "ВТПодчиненностьПодразделений1234" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТПодчиненностьПодразделений1234", ИмяВТПодчиненностьПодразделений1234);
	КонецЕсли;
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	// Укажем откуда надо брать сведения, которых нет в основной таблице запроса формирующего представление
	
	ИсточникиСведений = Новый Соответствие;
	ИсточникиСведений.Вставить("ПодразделениеВерхнегоУровня", "ПодразделенияВерхнегоУровня.ВышестоящееПодразделение");
	ИсточникиСведений.Вставить("Подразделение1", "Подразделения1.ВышестоящееПодразделение"); 
	ИсточникиСведений.Вставить("Подразделение2", "Подразделения2.ВышестоящееПодразделение");
	ИсточникиСведений.Вставить("Подразделение3", "Подразделения3.ВышестоящееПодразделение");
	ИсточникиСведений.Вставить("Подразделение4", СтрШаблон("%1.Подразделение", ИмяВТПодчиненностьПодразделений1234)); // имя основной таблицы могли поменять
	
	ПараметрыПостроения.Вставить("ИсточникиСведений", ИсточникиСведений);
	
	Возврат Запрос;

КонецФункции
	
#КонецОбласти


#Область Представление_ПодчиненностьПодразделенийОрганизаций1234

// Делает из запроса получения подчиненности подразделений компании запрос получения подчиненности подразделений организаций
Процедура ДоработатьЗапросДляПодчиненностиПодразделенийОрганизаций(ТекстЗапроса)
	
	// Получаем уровни подразделений организаций так как они есть в подчинености подразделений компании
	
	ТекстЗапросаПодразделенийОрганизаций = 
#Область ТекстЗапроса
		"ВЫБРАТЬ
		|	ПодчиненностьПодразделенийОрганизаций.Подразделение КАК Подразделение,
		|	СУММА(1) КАК Уровень
		|ПОМЕСТИТЬ ВТУровниПодразделенийОрганизаций
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодчиненностьПодразделенийОрганизаций.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодчиненностьПодразделенийОрганизаций.Подразделение КАК СтруктурнаяЕдиница,
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение КАК ВышестоящаяСтруктурнаяЕдиница,
		|	УровниПодразделенийОрганизацийМаксимальные.Уровень - УровниПодразделенийОрганизаций.Уровень КАК Уровень
		|ПОМЕСТИТЬ ВТПодчиненностьПодразделенийОрганизаций
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУровниПодразделенийОрганизаций КАК УровниПодразделенийОрганизаций
		|		ПО ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение = УровниПодразделенийОрганизаций.Подразделение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВТУровниПодразделенийОрганизаций.Подразделение КАК Подразделение,
		|			МАКСИМУМ(ВТУровниПодразделенийОрганизаций.Уровень) КАК Уровень
		|		ИЗ
		|			ВТУровниПодразделенийОрганизаций КАК ВТУровниПодразделенийОрганизаций
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ВТУровниПодразделенийОрганизаций.Подразделение) КАК УровниПодразделенийОрганизацийМаксимальные
		|		ПО ПодчиненностьПодразделенийОрганизаций.Подразделение = УровниПодразделенийОрганизацийМаксимальные.Подразделение";
#КонецОбласти
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(ТекстЗапросаПодразделенийОрганизаций, ТекстЗапроса);
	
	Схема = Новый СхемаЗапроса;
	
	Схема.УстановитьТекстЗапроса(ТекстЗапросаПодразделенийОрганизаций);
	
	ЗапросПредставления = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросСхемыФормирующийВТ(Схема, "ВТПодчиненностьПодразделений");
	
	ЗапросПредставления.Операторы[0].Источники.Заменить(0, ЗапросПредставления.ДоступныеТаблицы.Найти("ВТПодчиненностьПодразделенийОрганизаций"));
	
	ЗапросУничтожения = Схема.ПакетЗапросов.Добавить(Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
	ЗапросУничтожения.ИмяТаблицы = "ВТУровниПодразделенийОрганизаций";
	
	ЗапросУничтожения = Схема.ПакетЗапросов.Добавить(Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
	ЗапросУничтожения.ИмяТаблицы = "ВТПодчиненностьПодразделенийОрганизаций";
	
	ТекстЗапроса = Схема.ПолучитьТекстЗапроса();

КонецПроцедуры

#КонецОбласти

#КонецОбласти
